plugins {
    id 'com.palantir.docker'
    id 'java' // required for versioning
}

apply from: "../../gradle/versioning/versioning.gradle"

ext {
    docker_repo = 'datahub-elasticsearch-setup'
    docker_dir = 'elasticsearch-setup'
}

docker {
    name "${docker_registry}/${docker_repo}:v${version}"
    version "v${version}"
    dockerfile file("${rootProject.projectDir}/docker/${docker_dir}/Dockerfile")
    files fileTree(rootProject.projectDir) {
        include "docker/${docker_dir}/*"
        include "metadata-service/restli-servlet-impl/src/main/resources/index/**"
    }
    tag("Debug", "${docker_registry}/${docker_repo}:debug")

    // platform('linux/arm64', 'linux/amd64')
    buildx(true)
    load(true)
    push(false)
}
tasks.getByPath('docker').dependsOn('build')

task mkdirBuildDocker {
    doFirst {
        mkdir "${project.buildDir}/docker"
    }
}
dockerClean.finalizedBy(mkdirBuildDocker)

task cleanLocalDockerImages {
    doLast {
        rootProject.ext.cleanLocalDockerImages(docker_registry, docker_repo, "v${version}".toString())
    }
}
dockerClean.finalizedBy(cleanLocalDockerImages)